plugins {
	id 'com.gradle.plugin-publish' version '0.10.1'
	id 'com.github.ben-manes.versions' version '0.27.0'
	id 'com.diffplug.gradle.spotless' version '3.26.1'
	id 'com.diffplug.blowdryer'
}

apply from: 干.file('base/java8.gradle')
apply from: 干.file('base/gradle-plugin.gradle')
apply from: 干.file('base/maven.gradle')
apply from: 干.file('spotless/freshmark.gradle')
apply from: 干.file('spotless/java.gradle')

version = '0.1.0-SNAPSHOT'

dependencies {
	implementation 'com.squareup.okhttp3:okhttp:4.2.2'
	implementation 'com.squareup.okio:okio:2.4.1'
	implementation 'com.diffplug.durian:durian-core:1.2.0'
	implementation 'com.diffplug.durian:durian-io:1.2.0'
	testImplementation 'junit:junit:4.12'
	testImplementation 'org.assertj:assertj-core:3.14.0'
}

////////////////////////////
// Maven publishing setup //
////////////////////////////
publishing {
	publications {
		pluginMaven(MavenPublication) {
			groupId = maven_group
			artifactId = maven_artifact
			version = project.version

			pom {
				name = maven_name
				description = maven_description
				url = "https://$git_url"
				licenses {
					license {
						name = license_name
						url = license_url
					}
				}
				developers {
					developer {
						id = 'nedtwigg'
						name = 'Ned Twigg'
						email = 'ned.twigg@diffplug.com'
					}
				}
				scm {
					connection = "scm:git:git://${git_url}.git"
					developerConnection = "scm:git:ssh://${git_url}.git"
					url = "https://$git_url"
				}
			}
		}
	}
}
